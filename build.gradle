version = '0.8.0-SNAPSHOT'

ext.versions = [
    guava: '21.0',
    emf: '2.12.0'
]

configure(subprojects) {
	repositories {
		mavenCentral()
	}

	apply plugin: 'java'
    sourceCompatibility = '1.8'
	group = 'org.eclipse.sprotty'
	version = rootProject.version

    tasks.withType(JavaCompile) {
        options.encoding = 'UTF-8'
    }
    tasks.withType(Javadoc) {
        options.encoding = 'UTF-8'
        options.addStringOption('Xdoclint:none', '-quiet')
    }

    task sourcesJar(type: Jar, dependsOn: classes) {
        group 'Build'
        description 'Assembles a jar archive containing the sources'
        classifier = 'sources'
        from sourceSets.main.java
    }

    task javadocJar(type: Jar, dependsOn: javadoc) {
        group 'Build'
        description 'Assembles a jar archive containing the JavaDoc output'
        classifier = 'javadoc'
        from javadoc.destinationDir
    }

    artifacts.archives sourcesJar, javadocJar

    apply plugin: 'maven-publish'
}
